apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.sistemium"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 479
        versionName "1.1.93"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            // Filter for architectures supported by Flutter.
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".sissalesdebug"
            manifestPlaceholders = [appName: "SisSales-debug"]
            debuggable = true
        }
        release {
            applicationIdSuffix ".sissales"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [appName: "SisSales"]
        }
        driverDebug {
            initWith(buildTypes.debug)
            applicationIdSuffix ".driverdebug"
            manifestPlaceholders = [appName: "SisDriver-debug"]
            matchingFallbacks = ['debug']
        }
        driverRelease {
            initWith(buildTypes.release)
            applicationIdSuffix ".driver"
            manifestPlaceholders = [appName: "SisDriver"]
            matchingFallbacks = ['release']
        }
        warehouseDebug {
            initWith(buildTypes.debug)
            applicationIdSuffix ".warehousedebug"
            manifestPlaceholders = [appName: "SisWarehouse-debug"]
            matchingFallbacks = ['debug']
        }
        warehouseRelease {
            initWith(buildTypes.release)
            applicationIdSuffix ".warehouse"
            manifestPlaceholders = [appName: "SisWarehouse"]
            matchingFallbacks = ['release']
        }
        vfsDebug {
            initWith(buildTypes.debug)
            applicationIdSuffix = '.vfsdebug'
            manifestPlaceholders = [appName: 'VFS-debug']
            matchingFallbacks = ['debug']
        }
        vfsRelease {
            initWith(buildTypes.release)
            applicationIdSuffix = '.vfs'
            manifestPlaceholders = [appName: 'VFS']
            matchingFallbacks = ['release']
        }
        vfsClientDebug {
            initWith(buildTypes.debug)
            applicationIdSuffix = '.vfsclientdebug'
            manifestPlaceholders = [appName: 'VFSClient-debug']
            matchingFallbacks = ['debug']
        }
        vfsClientRelease {
            initWith(buildTypes.release)
            applicationIdSuffix = '.vfsclient'
            manifestPlaceholders = [appName: 'VFSClient']
            matchingFallbacks = ['release']
        }
        //https://docs.flutter.dev/development/add-to-app/android/project-setup#option-a---depend-on-the-android-archive-aar
        profile {
            initWith debug
        }

    }
    lint {
        checkReleaseBuilds false
    }
    namespace 'com.sistemium.sissales'
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility 11
        targetCompatibility 11
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    jcenter()
    maven {
        url '../../flutter_onboarding/build/host/outputs/repo'
    }
    maven {
        url 'https://storage.googleapis.com/download.flutter.io'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'nl.komponents.kovenant:kovenant:3.3.0'
    implementation 'com.github.kittinunf.fuel:fuel-android:1.15.0'
    implementation 'com.github.kittinunf.fuel:fuel-gson:1.15.0'
    implementation 'com.bandyer:socket.io-client:1.0.9'
    implementation 'com.github.EdgarJan:Secured-Preference-Store:-SNAPSHOT'
    implementation 'com.github.javiersantos:AppUpdater:master-SNAPSHOT'
    implementation 'com.google.android.gms:play-services-location:20.0.0'
    implementation project(path: ':BarcodeScannerLibrary')
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0"
    implementation 'com.google.mlkit:barcode-scanning:17.0.2'
    implementation "androidx.camera:camera-camera2:1.2.0-alpha03"
    implementation "androidx.camera:camera-lifecycle:1.2.0-alpha03"
    implementation "androidx.camera:camera-view:1.2.0-alpha03"
    debugImplementation 'com.sistemium.flutter_onboarding:flutter_debug:1.0'
    releaseImplementation 'com.sistemium.flutter_onboarding:flutter_release:1.0'
    warehouseDebugImplementation 'com.sistemium.flutter_onboarding:flutter_debug:1.0'
    warehouseReleaseImplementation 'com.sistemium.flutter_onboarding:flutter_release:1.0'
    driverDebugImplementation 'com.sistemium.flutter_onboarding:flutter_debug:1.0'
    driverReleaseImplementation 'com.sistemium.flutter_onboarding:flutter_release:1.0'
    vfsDebugImplementation 'com.sistemium.flutter_onboarding:flutter_debug:1.0'
    vfsReleaseImplementation 'com.sistemium.flutter_onboarding:flutter_release:1.0'
}
